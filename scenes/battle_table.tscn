[gd_scene load_steps=7 format=3 uid="uid://1elqayaj0cj6"]

[ext_resource type="PackedScene" uid="uid://d3we480ibjoa8" path="res://scenes/card.tscn" id="1_sqmq8"]
[ext_resource type="Texture2D" uid="uid://bfqtrrndfk5fb" path="res://sprites/cartinha_mesa_back.png" id="2_7xvee"]
[ext_resource type="Texture2D" uid="uid://nl43x31aaxrd" path="res://sprites/Riso_preencher.png" id="5_4ib00"]
[ext_resource type="Texture2D" uid="uid://b1e4xfiddc4jo" path="res://sprites/cenario.png" id="6_6u686"]
[ext_resource type="Texture2D" uid="uid://x5c24tyujtj7" path="res://sprites/mesa_bar.png" id="6_kp2p7"]

[sub_resource type="GDScript" id="GDScript_si2f6"]
script/source = "extends Control

# --- Signals ---
# --- Enums ---
# --- Constants ---


# --- Exported Variables ---
@export var _card_preload: PackedScene
@export var _enemy_hand_container: Node2D
@export var _player_hand_container: Node2D
@export var _player_deck_node: Sprite2D
@export var _enemy_deck_node: Sprite2D
@export var _player_used_card_position: Marker2D
@export var _enemy_used_card_position: Marker2D
@export var _player_position: Marker2D
@export var _enemy_position: Marker2D
@export var _player_hand_pos_container: Node2D
@export var _enemy_hand_pos_container: Node2D


# --- Public Variables ---


# --- Private Variables ---
var _player_hand_positions := {}
var _enemy_hand_positions := {}

var _test_card := {
	\"name\": \"Card\", \"text\": \"Test\", \"type\": Constants.JokesTypes.PIADINHAS,
	\"rarity\": 1, \"cost\": 1, \"base_force\": 1
}

var _test_card_2 := {
	\"name\": \"Card\", \"text\": \"Test 2\", \"type\": Constants.JokesTypes.TOC_TOC,
	\"rarity\": 1, \"cost\": 1
}

var _card_size := Vector2(104, 152)
var _player_obj: Character
var _enemy_obj: Character
var _player_cards_count := 0
var _enemy_cards_count := 0
var _player_deck := []
var _enemy_deck := []
var _player_discard_pile := []
var _enemy_discard_pile := []
var _current_action := \"\"
var _action_owner: Character


# --- Onready Variables ---


# --- Engine Callbacks ---
func _ready() -> void:
	add_to_group(\"battle_table\")
	
	for node in _player_hand_pos_container.get_children():
		_player_hand_positions[node.name.to_int()] = node.global_position
	
	for node in _enemy_hand_pos_container.get_children():
		_enemy_hand_positions[node.name.to_int()] = node.global_position
	
	var player = preload(\"res://scenes/character.tscn\").instantiate()
	var enemy = preload(\"res://scenes/character.tscn\").instantiate()
	
	var player_data = Constants.TEST_PLAYER_DATA.duplicate(true)
	var enemy_data = Constants.TEST_PLAYER_DATA.duplicate(true)
	
	enemy_data[\"name\"] = \"Enemy\"
	enemy_data[\"sprite\"] = \"res://sprites/Enemy.png\"
	enemy_data[\"draw_amount\"] = 1
	
	player_data[\"draw_amount\"] = 1
	var card_factory := CardFactory.new()
	
	for i in range(0, 15):
#		player_data[\"deck\"].append(_test_card.duplicate(true))
#		enemy_data[\"deck\"].append(_test_card_2.duplicate(true))
		player_data[\"deck\"].append(card_factory.get_card_info())
		enemy_data[\"deck\"].append(_test_card_2.duplicate(true))
	
	player.load_player_data(player_data)
	enemy.load_player_data(enemy_data)
	
	prepare_battle(player, enemy)


func _input(p_event: InputEvent) -> void:
	if p_event is InputEventKey and p_event.is_pressed():
		if p_event.keycode == KEY_SPACE:
			_draw_new_card(_player_obj, _test_card.duplicate(true))
		
		if p_event.keycode == KEY_Z:
			_draw_new_card(_enemy_obj, _test_card_2.duplicate(true))


# --- Public Functions ---
func prepare_battle(p_player: Character, p_enemy: Character) -> void:
	add_child(p_enemy)
	add_child(p_player)
	
	_player_obj = p_player
	p_player.set_character_type(\"player\")
	
	_enemy_obj = p_enemy
	
	p_player.position = _player_position.position
	p_enemy.position = _enemy_position.position
	_player_deck = p_player.deck.duplicate(true)
	_enemy_deck = p_enemy.deck.duplicate(true)
	_player_deck.shuffle()
	_enemy_deck.shuffle()
	
	_new_turn()


func request_card_use(p_card: Node2D) -> void:
	if p_card.card_owner != _action_owner:
		return
	
	var final_pos
	
	if p_card.card_owner == _player_obj:
		p_card.reparent(_player_used_card_position)
		final_pos = _player_used_card_position.global_position
		_player_cards_count -= 1
		_disconnect_player_card_signals(p_card)
		p_card.prepare_to_use()
	
	else:
		_enemy_cards_count -= 1
		p_card.reparent(_enemy_used_card_position)
		final_pos = _enemy_used_card_position.global_position
		p_card.scale = Vector2(0.3, 0.3)
	
	p_card.z_index = 3
	_organize_hand(p_card.card_owner)
	await _do_card_tween(p_card, final_pos, Vector2(0.3, 0.3))
	p_card.z_index = 1
	
	if p_card.card_owner == _enemy_obj:
		p_card.flip(false, true)
		_enemy_discard_pile.append(p_card.get_card_data())
		$gui/player_health.text = str(_player_obj.receive_damage(p_card))
	
	else:
		_player_discard_pile.append(p_card.get_card_data())
		$gui/enemy_health.text = str(_enemy_obj.receive_damage(p_card))


# --- Private Functions ---
# - Common Use Functions -
func _draw_new_card(p_who: Character, p_card_info: Dictionary) -> void:
	var new_card = _card_preload.instantiate()
	var start_position
	var final_scale
	var final_pos
	
	if p_who == _player_obj:
		_player_cards_count += 1
		start_position = _player_deck_node.global_position
		_connect_player_card_signals(new_card)
		final_pos = _player_hand_positions[_player_cards_count]
		final_scale = Vector2(1, 1)
	
	else:
		_enemy_cards_count += 1
		start_position = _enemy_deck_node.global_position
		final_pos = _enemy_hand_positions[_enemy_cards_count]
		final_scale = Vector2(0.3, 0.3)
	
	new_card.card_owner = p_who
	
	new_card.global_position = start_position
	new_card.set_card_to(p_card_info)
	add_child(new_card)
	
	await _do_card_tween(new_card, final_pos, final_scale)
	
	if new_card.card_owner == _player_obj:
		new_card.is_on_hand = true
		await new_card.flip()
		new_card.reparent(_player_hand_container)
	
	else:
		new_card.reparent(_enemy_hand_container)


func _new_turn() -> void:
	await _discard_characters_cards()
	_draw_characters_cards()
	_action_owner = _player_obj


func _draw_characters_cards() -> void:
	if _player_deck.size() < _player_obj.draw_amount:
		await _reset_deck(_player_obj)
	
	if _enemy_deck.size() < _enemy_obj.draw_amount:
		await _reset_deck(_enemy_obj)
	
	for _i in range(0, _player_obj.draw_amount):
		_draw_new_card(_player_obj, _player_deck.pop_front())
	
	for _i in range(0, _enemy_obj.draw_amount):
		_draw_new_card(_enemy_obj, _enemy_deck.pop_front())


func _discard_characters_cards() -> void:
	# Avoid erros in mouse exited signal
	for card in _player_hand_container.get_children():
		card.prepare_to_use()
	
	await get_tree().create_timer(0.3).timeout

	for card in _player_hand_container.get_children():
		_player_discard_pile.append(card.get_card_data())
		card.queue_free()
	
	for card in _enemy_hand_container.get_children():
		_enemy_discard_pile.append(card.get_card_data())
		card.queue_free()
	
	_player_cards_count = 0
	_enemy_cards_count = 0


func _reset_deck(p_who: Character) -> void:
	var deck: Array
	var used_pile: Array
	
	if p_who == _player_obj:
		deck = _player_deck
		used_pile = _player_discard_pile
	
	else:
		deck = _enemy_deck
		used_pile = _enemy_discard_pile
	
	for card in used_pile:
		deck.append(card)
	
	used_pile.clear()
	deck.shuffle()


func _organize_hand(p_who: Character) -> void:
	var hand_container
	var positions
	
	if p_who == _player_obj:
		hand_container = _player_hand_container
		positions = _player_hand_positions
	
	else:
		hand_container = _enemy_hand_container
		positions = _enemy_hand_positions
	
	var count = 1
	
	for card in hand_container.get_children():
		card.global_position = positions[count]
		count += 1


func _enemy_turn() -> void:
	_action_owner = _enemy_obj
	await get_tree().create_timer(1.0).timeout
	
	if _enemy_cards_count > 0:
		var card = _enemy_hand_container.get_child(0)
		await request_card_use(card)
	
	_new_turn()


func _do_card_tween(p_card: Node2D, p_final_pos: Vector2, 
		p_final_scale: Vector2) -> void:
	
	var tween = create_tween().set_parallel(true)
	tween.tween_property(p_card, \"global_position\", p_final_pos, 0.4)
	tween.tween_property(p_card, \"scale\", p_final_scale, 0.4)
	await tween.finished


func _connect_player_card_signals(p_card: Node2D) -> void:
	var child = p_card.get_child(1)
	
	child.gui_input.connect(_on_player_card_gui_input.bind(p_card))
	child.mouse_entered.connect(_on_player_card_mouse_entered.bind(p_card))
	child.mouse_exited.connect(_on_player_card_mouse_exited.bind(p_card))


func _disconnect_player_card_signals(p_card: Node2D) -> void:
	var child = p_card.get_child(1)
	
	child.gui_input.disconnect(_on_player_card_gui_input)
	child.mouse_entered.disconnect(_on_player_card_mouse_entered)
	child.mouse_exited.disconnect(_on_player_card_mouse_exited)


# - Setget Functions -


# - Signal Functions -
func _on_player_card_gui_input(p_event: InputEvent, p_card: Node2D) -> void:
	if p_card.is_on_hand == false:
		return
	
	if p_event is InputEventMouseButton:
		if p_event.button_index == MOUSE_BUTTON_LEFT and p_event.pressed:
			request_card_use(p_card)


func _on_player_card_mouse_entered(p_card: Node2D) -> void:
	if p_card.is_on_hand == false:
		return
	
	for card in _player_hand_container.get_children():
		if card != p_card:
			card.z_index = 0
		
		else:
			card.z_index = 3
	
	var tween = create_tween()
	tween.tween_property(p_card, \"scale\", Vector2(1.5, 1.5), 0.2)
	await tween.finished
	p_card.z_index = 3


func _on_player_card_mouse_exited(p_card: Node2D) -> void:
	if p_card.is_on_hand == false:
		return
	
	var tween = create_tween()
	tween.tween_property(p_card, \"scale\", Vector2(1.0, 1.0), 0.2)
	await tween.finished
	p_card.z_index = 0


func _on_end_turn_button_pressed() -> void:
	_enemy_turn()
"

[node name="battle_table" type="Control" node_paths=PackedStringArray("_enemy_hand_container", "_player_hand_container", "_player_deck_node", "_enemy_deck_node", "_player_used_card_position", "_enemy_used_card_position", "_player_position", "_enemy_position", "_player_hand_pos_container", "_enemy_hand_pos_container")]
layout_mode = 3
anchors_preset = 0
offset_right = 1280.0
offset_bottom = 720.0
mouse_filter = 2
script = SubResource("GDScript_si2f6")
_card_preload = ExtResource("1_sqmq8")
_enemy_hand_container = NodePath("player_hand_container")
_player_hand_container = NodePath("enemy_hand_container")
_player_deck_node = NodePath("MesaBar/player_deck")
_enemy_deck_node = NodePath("MesaBar/enemy_deck")
_player_used_card_position = NodePath("used_player_card_position")
_enemy_used_card_position = NodePath("used_enemy_card_position")
_player_position = NodePath("player_position")
_enemy_position = NodePath("enemy_position")
_player_hand_pos_container = NodePath("player_cards_positions")
_enemy_hand_pos_container = NodePath("enemy_cards_positions")
metadata/_edit_lock_ = true
metadata/_edit_vertical_guides_ = []

[node name="Cenario" type="TextureRect" parent="."]
z_index = -2
layout_mode = 1
anchors_preset = 8
anchor_left = 0.5
anchor_top = 0.5
anchor_right = 0.5
anchor_bottom = 0.5
offset_left = -640.0
offset_top = -360.0
offset_right = 640.0
offset_bottom = 360.0
grow_horizontal = 2
grow_vertical = 2
size_flags_horizontal = 4
mouse_filter = 2
texture = ExtResource("6_6u686")
metadata/_edit_lock_ = true

[node name="Camera2D" type="Camera2D" parent="."]
position = Vector2(640, 360)

[node name="MesaBar" type="Sprite2D" parent="."]
position = Vector2(498, 365)
texture = ExtResource("6_kp2p7")

[node name="player_deck" type="Sprite2D" parent="MesaBar"]
z_index = 1
position = Vector2(-177, 230)
texture = ExtResource("2_7xvee")

[node name="enemy_deck" type="Sprite2D" parent="MesaBar"]
z_index = 1
position = Vector2(-174, 106)
texture = ExtResource("2_7xvee")

[node name="gui" type="CanvasLayer" parent="."]

[node name="end_turn_button" type="Button" parent="gui"]
offset_left = 33.0
offset_top = 638.0
offset_right = 187.0
offset_bottom = 700.0
text = "End Turn"

[node name="player_health" type="Label" parent="gui"]
offset_left = 44.0
offset_top = 501.0
offset_right = 210.0
offset_bottom = 582.0
text = "Enemy"
horizontal_alignment = 1
vertical_alignment = 1

[node name="enemy_health" type="Label" parent="gui"]
offset_left = 773.0
offset_top = 316.0
offset_right = 939.0
offset_bottom = 397.0
text = "Enemy"
horizontal_alignment = 1
vertical_alignment = 1

[node name="player_status" type="Control" parent="gui"]
layout_mode = 3
anchors_preset = 0
offset_right = 40.0
offset_bottom = 40.0

[node name="enemy_status" type="Control" parent="gui"]
layout_mode = 3
anchors_preset = 0
offset_left = 402.0
offset_top = 326.0
offset_right = 442.0
offset_bottom = 366.0

[node name="health_bar" type="TextureProgressBar" parent="gui/enemy_status"]
layout_mode = 0
offset_right = 297.0
offset_bottom = 40.0
value = 50.0
nine_patch_stretch = true
texture_progress = ExtResource("5_4ib00")

[node name="used_player_card_position" type="Marker2D" parent="."]
z_index = 1
position = Vector2(387, 593)
gizmo_extents = 0.0

[node name="used_enemy_card_position" type="Marker2D" parent="."]
z_index = 1
position = Vector2(384, 470)
gizmo_extents = 0.0

[node name="player_position" type="Marker2D" parent="."]
position = Vector2(456, 665)

[node name="enemy_position" type="Marker2D" parent="."]
position = Vector2(571, 360)

[node name="player_cards_positions" type="Node2D" parent="."]
position = Vector2(806, 543)

[node name="1" type="Marker2D" parent="player_cards_positions"]

[node name="2" type="Marker2D" parent="player_cards_positions"]
position = Vector2(100, 0)

[node name="3" type="Marker2D" parent="player_cards_positions"]
position = Vector2(200, 0)

[node name="4" type="Marker2D" parent="player_cards_positions"]
position = Vector2(300, 0)

[node name="5" type="Marker2D" parent="player_cards_positions"]
position = Vector2(400, 0)

[node name="enemy_cards_positions" type="Node2D" parent="."]
position = Vector2(518, 457)

[node name="1" type="Marker2D" parent="enemy_cards_positions"]

[node name="2" type="Marker2D" parent="enemy_cards_positions"]
position = Vector2(40, 0)

[node name="3" type="Marker2D" parent="enemy_cards_positions"]
position = Vector2(80, 0)

[node name="4" type="Marker2D" parent="enemy_cards_positions"]
position = Vector2(120, 0)

[node name="5" type="Marker2D" parent="enemy_cards_positions"]
position = Vector2(160, 0)

[node name="player_hand_container" type="Node2D" parent="."]

[node name="enemy_hand_container" type="Node2D" parent="."]

[node name="TextureProgressBar" type="TextureProgressBar" parent="."]
layout_mode = 0
offset_left = 436.0
offset_top = 353.0
offset_right = 506.0
offset_bottom = 393.0
texture_progress = ExtResource("5_4ib00")

[connection signal="pressed" from="gui/end_turn_button" to="." method="_on_end_turn_button_pressed"]
