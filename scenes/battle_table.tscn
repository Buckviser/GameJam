[gd_scene load_steps=6 format=3 uid="uid://1elqayaj0cj6"]

[ext_resource type="PackedScene" uid="uid://d3we480ibjoa8" path="res://scenes/card.tscn" id="1_sqmq8"]
[ext_resource type="Texture2D" uid="uid://bfqtrrndfk5fb" path="res://sprites/cartinha_mesa_back.png" id="2_7xvee"]
[ext_resource type="Texture2D" uid="uid://b1e4xfiddc4jo" path="res://sprites/cenario.png" id="6_6u686"]
[ext_resource type="Texture2D" uid="uid://x5c24tyujtj7" path="res://sprites/mesa_bar.png" id="6_kp2p7"]

[sub_resource type="GDScript" id="GDScript_si2f6"]
script/source = "extends Control

# --- Signals ---
# --- Enums ---
# --- Constants ---


# --- Exported Variables ---
@export var _card_preload: PackedScene
@export var _enemy_hand_container: HBoxContainer
@export var _player_hand_container: HBoxContainer
@export var _player_deck_node: TextureRect
@export var _enemy_deck_node: TextureRect
@export var _used_card_final_position: Marker2D
@export var _player_position: Marker2D
@export var _enemy_position: Marker2D


# --- Public Variables ---


# --- Private Variables ---
var hand_container_offset := {
	1: Vector2(225, 0), 2: Vector2(279, 0), 3: Vector2(333, 0), 
	4: Vector2(387, 0), 5: Vector2(441, 0)}

var enemy_hand_offset := {
	1: Vector2(10, -50), 2: Vector2(30, -50), 3: Vector2(50, -50), 
	4: Vector2(70, -50), 5: Vector2(90, -50)
}

var _test_card := {
	\"name\": \"Card\", \"text\": \"Test\", \"type\": Constants.JokesTypes.PIADINHAS,
	\"rarity\": 1, \"cost\": 1, \"base_force\": 1
}

var _test_card_2 := {
	\"name\": \"Card\", \"text\": \"Test 2\", \"type\": Constants.JokesTypes.TOC_TOC,
	\"rarity\": 1, \"cost\": 1
}

var _card_size := Vector2(104, 152)
var _player_obj: Character
var _enemy_obj: Character
var _player_cards_count := 0
var _enemy_cards_count := 0
var _player_deck := []
var _enemy_deck := []
var _player_discard_pile := []
var _enemy_discard_pile := []
var _current_action := \"\"
var _action_owner: Character


# --- Onready Variables ---


# --- Engine Callbacks ---
func _ready() -> void:
	add_to_group(\"battle_table\")
	
	var player = preload(\"res://scenes/character.tscn\").instantiate()
	var enemy = preload(\"res://scenes/character.tscn\").instantiate()
	
	var player_data = Constants.TEST_PLAYER_DATA.duplicate(true)
	var enemy_data = Constants.TEST_PLAYER_DATA.duplicate(true)
	
	enemy_data[\"name\"] = \"Enemy\"
	enemy_data[\"sprite\"] = \"res://sprites/Enemy.png\"
	enemy_data[\"draw_amount\"] = 1
	
	player_data[\"draw_amount\"] = 1
	
	for i in range(0, 15):
		player_data[\"deck\"].append(_test_card.duplicate(true))
		enemy_data[\"deck\"].append(_test_card_2.duplicate(true))
	
	player.load_player_data(player_data)
	enemy.load_player_data(enemy_data)
	
	prepare_battle(player, enemy)


func _input(p_event: InputEvent) -> void:
	if p_event is InputEventKey and p_event.is_pressed():
		if p_event.keycode == KEY_SPACE:
			_draw_new_card(_player_obj, _test_card.duplicate(true))
		
		if p_event.keycode == KEY_Z:
			_draw_new_card(_enemy_obj, _test_card_2.duplicate(true))


# --- Public Functions ---
func prepare_battle(p_player: Character, p_enemy: Character) -> void:
	add_child(p_enemy)
	add_child(p_player)
	
	_player_obj = p_player
	p_player.set_character_type(\"player\")
	
	_enemy_obj = p_enemy
	
	p_player.position = _player_position.position
	p_enemy.position = _enemy_position.position
	_player_deck = p_player.deck.duplicate(true)
	_enemy_deck = p_enemy.deck.duplicate(true)
	_player_deck.shuffle()
	_enemy_deck.shuffle()
	
	_new_turn()


func request_card_use(p_card: Control) -> void:
	if p_card.card_owner != _action_owner:
		return
	
	var final_pos
	
	p_card.reparent($used_cards_pile, false)
	if p_card.card_owner == _player_obj:
		_player_cards_count -= 1
		_disconnect_player_card_signals(p_card)
		final_pos = $used_player_card_position.global_position
		p_card.prepare_to_use()
	
	else:
		_enemy_cards_count -= 1
		final_pos = $used_enemy_card_position.global_position
		p_card.scale = Vector2(0.3, 0.3)
	
	p_card.z_index = 3
	
	await _do_card_tween(p_card, final_pos, Vector2(0.3, 0.3))
	p_card.z_index = 1
	
	if p_card.card_owner == _enemy_obj:
		p_card.flip(false, true)
		_enemy_discard_pile.append(p_card.get_card_data())
	
	else:
		_player_discard_pile.append(p_card.get_card_data())


# --- Private Functions ---
# - Common Use Functions -
func _draw_new_card(p_who: Character, p_card_info: Dictionary) -> void:
	var new_card = _card_preload.instantiate()
	var start_position
	var final_scale
	var final_pos
	
	if p_who == _player_obj:
		_player_cards_count += 1
		start_position = $player_card_start.global_position
		_connect_player_card_signals(new_card)
		final_pos = _player_hand_container.global_position + hand_container_offset[_player_cards_count]
		final_scale = Vector2(1, 1)
	
	else:
		_enemy_cards_count += 1
		start_position = $enemy_card_start.global_position
		final_pos = _enemy_hand_container.global_position + enemy_hand_offset[_enemy_cards_count]
		final_scale = Vector2(0.3, 0.3)
	
	new_card.card_owner = p_who
	
	new_card.global_position = start_position
	new_card.set_card_to(p_card_info)
	add_child(new_card)
	
	await _do_card_tween(new_card, final_pos, final_scale)
	
	if new_card.card_owner == _player_obj:
		new_card.is_on_hand = true
		await new_card.flip()
		new_card.reparent(_player_hand_container, false)
	
	else:
		new_card.reparent(_enemy_hand_container, false)
	
	print(new_card.global_position)


func _new_turn() -> void:
	await _discard_characters_cards()
	_draw_characters_cards()
	_action_owner = _player_obj


func _draw_characters_cards() -> void:
	if _player_deck.size() < _player_obj.draw_amount:
		await _reset_deck(_player_obj)
	
	if _enemy_deck.size() < _enemy_obj.draw_amount:
		await _reset_deck(_enemy_obj)
	
	for _i in range(0, _player_obj.draw_amount):
		_draw_new_card(_player_obj, _player_deck.pop_front())
	
	for _i in range(0, _enemy_obj.draw_amount):
		_draw_new_card(_enemy_obj, _enemy_deck.pop_front())


func _discard_characters_cards() -> void:
	# Avoid erros in mouse exited signal
	for card in _player_hand_container.get_children():
		card.prepare_to_use()
	
	await get_tree().create_timer(0.3).timeout

	for card in _player_hand_container.get_children():
		_player_discard_pile.append(card.get_card_data())
		card.queue_free()
	
	for card in _enemy_hand_container.get_children():
		_enemy_discard_pile.append(card.get_card_data())
		card.queue_free()
	
	_player_cards_count = 0
	_enemy_cards_count = 0


func _reset_deck(p_who: Character) -> void:
	var deck: Array
	var used_pile: Array
	
	if p_who == _player_obj:
		deck = _player_deck
		used_pile = _player_discard_pile
	
	else:
		deck = _enemy_deck
		used_pile = _enemy_discard_pile
	
	for card in used_pile:
		deck.append(card)
	
	used_pile.clear()
	deck.shuffle()


func _enemy_turn() -> void:
	_action_owner = _enemy_obj
	await get_tree().create_timer(1.0).timeout
	
	if _enemy_cards_count > 0:
		var card = _enemy_hand_container.get_child(0)
		await request_card_use(card)
	
	_new_turn()


func _do_card_tween(p_card: Control, p_final_pos: Vector2, 
		p_final_scale: Vector2) -> void:
	
	var tween = create_tween().set_parallel(true)
	tween.tween_property(p_card, \"global_position\", p_final_pos, 0.4)
	tween.tween_property(p_card, \"scale\", p_final_scale, 0.4)
	await tween.finished


func _connect_player_card_signals(p_card: Control) -> void:
	p_card.gui_input.connect(_on_player_card_gui_input.bind(p_card))
	p_card.mouse_entered.connect(_on_player_card_mouse_entered.bind(p_card))
	p_card.mouse_exited.connect(_on_player_card_mouse_exited.bind(p_card))


func _disconnect_player_card_signals(p_card: Control) -> void:
	p_card.gui_input.disconnect(_on_player_card_gui_input)
	p_card.mouse_entered.disconnect(_on_player_card_mouse_entered)
	p_card.mouse_exited.disconnect(_on_player_card_mouse_exited)


# - Setget Functions -


# - Signal Functions -
func _on_player_card_gui_input(p_event: InputEvent, p_card: Control) -> void:
	if p_card.is_on_hand == false:
		return
	
	if p_event is InputEventMouseButton:
		if p_event.button_index == MOUSE_BUTTON_LEFT and p_event.pressed:
			request_card_use(p_card)


func _on_player_card_mouse_entered(p_card: Control) -> void:
	if p_card.is_on_hand == false:
		return
	
	for card in _player_hand_container.get_children():
		if card != p_card:
			card.z_index = 0
		
		else:
			card.z_index = 3
	
	var tween = create_tween()
	tween.tween_property(p_card, \"scale\", Vector2(1.5, 1.5), 0.2)
	await tween.finished
	p_card.z_index = 3


func _on_player_card_mouse_exited(p_card: Control) -> void:
	if p_card.is_on_hand == false:
		return
	
	var tween = create_tween()
	tween.tween_property(p_card, \"scale\", Vector2(1.0, 1.0), 0.2)
	await tween.finished
	p_card.z_index = 0


func _on_end_turn_button_pressed() -> void:
	_enemy_turn()
"

[node name="battle_table" type="Control" node_paths=PackedStringArray("_enemy_hand_container", "_player_hand_container", "_player_deck_node", "_enemy_deck_node", "_used_card_final_position", "_player_position", "_enemy_position")]
layout_mode = 3
anchors_preset = 0
offset_right = 1280.0
offset_bottom = 720.0
mouse_filter = 2
script = SubResource("GDScript_si2f6")
_card_preload = ExtResource("1_sqmq8")
_enemy_hand_container = NodePath("enemy_hand_container")
_player_hand_container = NodePath("player_hand_container")
_player_deck_node = NodePath("MesaBar/player_deck")
_enemy_deck_node = NodePath("MesaBar/enemy_deck")
_used_card_final_position = NodePath("used_enemy_card_position")
_player_position = NodePath("player_position")
_enemy_position = NodePath("enemy_position")
metadata/_edit_lock_ = true

[node name="player_hand_container" type="HBoxContainer" parent="."]
z_index = 2
layout_mode = 0
offset_left = 547.0
offset_top = 496.0
offset_right = 1015.0
offset_bottom = 588.0
size_flags_horizontal = 4
size_flags_vertical = 4
alignment = 1

[node name="enemy_hand_container" type="HBoxContainer" parent="."]
z_index = 3
layout_mode = 0
offset_left = 443.0
offset_top = 431.0
offset_right = 684.0
offset_bottom = 480.0
scale = Vector2(0.3, 0.3)
size_flags_horizontal = 4
size_flags_vertical = 4
alignment = 1

[node name="used_cards_pile" type="Control" parent="."]
layout_mode = 3
anchors_preset = 0
offset_left = -199.0
offset_top = 5.0
offset_right = -159.0
offset_bottom = 45.0

[node name="Cenario" type="TextureRect" parent="."]
z_index = -2
layout_mode = 1
anchors_preset = 8
anchor_left = 0.5
anchor_top = 0.5
anchor_right = 0.5
anchor_bottom = 0.5
offset_left = -640.0
offset_top = -360.0
offset_right = 640.0
offset_bottom = 360.0
grow_horizontal = 2
grow_vertical = 2
size_flags_horizontal = 4
mouse_filter = 2
texture = ExtResource("6_6u686")
metadata/_edit_lock_ = true

[node name="Camera2D" type="Camera2D" parent="."]
position = Vector2(640, 360)

[node name="MesaBar" type="Sprite2D" parent="."]
position = Vector2(498, 365)
texture = ExtResource("6_kp2p7")

[node name="player_deck" type="TextureRect" parent="MesaBar"]
z_index = 1
offset_left = 176.0
offset_top = 207.0
offset_right = 216.0
offset_bottom = 257.0
texture = ExtResource("2_7xvee")

[node name="enemy_deck" type="TextureRect" parent="MesaBar"]
z_index = 1
offset_left = -193.0
offset_top = 82.0
offset_right = -153.0
offset_bottom = 132.0
texture = ExtResource("2_7xvee")

[node name="gui" type="CanvasLayer" parent="."]

[node name="end_turn_button" type="Button" parent="gui"]
offset_left = 33.0
offset_top = 638.0
offset_right = 187.0
offset_bottom = 700.0
text = "End Turn"

[node name="used_player_card_position" type="Marker2D" parent="."]
z_index = 1
position = Vector2(567, 518)
gizmo_extents = 0.0

[node name="used_enemy_card_position" type="Marker2D" parent="."]
z_index = 1
position = Vector2(340, 399)
gizmo_extents = 0.0

[node name="player_position" type="Marker2D" parent="."]
position = Vector2(456, 665)

[node name="enemy_position" type="Marker2D" parent="."]
position = Vector2(571, 360)

[node name="player_card_start" type="Marker2D" parent="."]
position = Vector2(638, 518)
scale = Vector2(0.3, 0.3)

[node name="enemy_card_start" type="Marker2D" parent="."]
position = Vector2(269, 392)
scale = Vector2(0.3, 0.3)

[connection signal="pressed" from="gui/end_turn_button" to="." method="_on_end_turn_button_pressed"]
